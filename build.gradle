plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/central' }
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:23.0.0'
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

processResources {
    filesMatching('plugin.properties') {
        expand(project.properties)
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT'

    implementation 'com.saicone.ezlib:loader:1.2.3'

    compileOnly ('redis.clients:jedis:4.2.0') {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'org.json', module: 'json'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    compileOnly ('com.rabbitmq:amqp-client:5.14.2') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}

jar {
    enabled = false
    dependsOn (shadowJar)
}

shadowJar {
    archiveClassifier.set('')
    relocate 'com.saicone.ezlib', project.group + '.lib.ezlib'
    relocate 'org.apache.commons.pool2', project.group + '.lib.pool2'
    relocate 'org.slf4j', project.group + '.lib.slf4j'
    relocate 'redis.clients.jedis', project.group + '.lib.jedis'
    relocate 'com.rabbitmq', project.group + '.lib.rabbitmq'
}