plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        maven {
            url 'http://ptms.ink:8081/repository/releases/'
            allowInsecureProtocol true
        }
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:23.0.0'
    }

    compileJava {
        options.release = 8
        options.encoding = 'UTF-8'
    }
}

processResources {
    filesMatching('plugin.properties') {
        expand(project.properties)
    }
}

dependencies {
    compileOnly 'ink.ptms.core:v11802:11802:universal'
    compileOnly 'ink.ptms.core:v11701:11701:universal'

    implementation 'com.saicone.ezlib:ezlib:1.1'

    compileOnly ('redis.clients:jedis:4.2.0') {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'org.json', module: 'json'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    compileOnly ('com.rabbitmq:amqp-client:5.14.2') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}

jar {
    enabled = false
    dependsOn (shadowJar)
}

shadowJar {
    archiveClassifier.set('')
    relocate 'com.saicone.ezlib', project.group + '.lib.ezlib'
    relocate 'com.google.gson', project.group + '.lib.gson'
    relocate 'org.apache.commons.pool2', project.group + '.lib.pool2'
    relocate 'org.json', project.group + '.lib.json'
    relocate 'org.slf4j', project.group + '.lib.slf4j'
    relocate 'redis.clients.jedis', project.group + '.lib.jedis'
    relocate 'com.rabbitmq', project.group + '.lib.rabbitmq'
}